plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
}

group = 'com.github.jorepong'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT")
    compileOnly "dev.jorel:commandapi-annotations:9.5.2"

    // CommandAPI 버전 셰이딩
    implementation "dev.jorel:commandapi-bukkit-shade:9.5.2"
    annotationProcessor "dev.jorel:commandapi-annotations:9.5.2"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependencies {
        include dependency("dev.jorel:commandapi-bukkit-shade:9.5.2")  // CommandAPI 포함
    }

    // 패키지 이동 설정 - 원하는 패키지명으로 변경
    relocate("dev.jorel.commandapi", "com.github.jorepong.flexibleProperty.commandapi")
}
